set term postscript enhanced solid color eps size 5,7
set output 'residentialOutput.eps'

load 'dates.gpl'

fn = 'residentialOutput.dat'

startDay = -14
endDay = 29
statCutoff = 10 

set key noauto

set ylabel "%"

set multiplot layout 2,1 


set label 1 'PRELIMINARY' at  5,50 rotate by 30 center textcolor rgb "#999999" font "Helvetica,80"

set title 'Residential address-fraction'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$3*100 :1/0):(sqrt( $3 * (1-$3) / $2 )*100) w yerr lt 3 pt 7 

unset ylabel 
set logsc y

set label 5 'low-statistics cutoff' at -3,(statCutoff*0.65) center front
set title 'Number of still-approved entries daily'
pl [startDay:endDay][1:]\
	fn u 1:2  w p lt -1 pt 7,\
	statCutoff lt 0

unset multiplot
