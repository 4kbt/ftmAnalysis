set term postscript enhanced solid color eps size 5,7
set output 'needs.eps'

set colors classic

set xtics ("3/20" -11, "3/24" -7, "3/28" -3,  "4/1" 1, "4/5" 5, "4/9" 9, "4/13" 13, "4/17" 17, "4/21" 21)

fn = 'needs.dat'

startDay = -14
endDay = 19
statCutoff = 10 

set key noauto

set ylabel "%"

set multiplot layout 6,2  title 'Percentage of institutions expressing PPE need in FindTheMasks data.'

#set label 1 'PRELIMINARY' at screen 0.5,0.5 rotate by 60 center front textcolor rgb "#999999" font "Helvetica,80"

set title 'N95 Mask'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$3*100 :1/0):(sqrt( $3 * (1-$3) / $2 )*100) w yerr lt 3 pt 7 

set title 'Surgical Mask'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$4*100 :1/0):(sqrt( $4 * (1-$4) / $2 )*100) w yerr lt 3 pt 7 

set title 'Homemade Masks'
pl [startDay:endDay][0:100] fn u ($1>-2?$1:1/0):($2>statCutoff?$11*100 :1/0):(sqrt( $11 * (1-$11) / $2 )*100) w yerr lt 3 pt 7 

set title 'Gloves'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$5*100 :1/0):(sqrt( $5 * (1-$5) / $2 )*100) w yerr lt 3 pt 7

set title 'Gowns'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$6*100 :1/0):(sqrt( $6 * (1-$6) / $2 )*100) w yerr lt 3 pt 7

set title 'Hand Sanitizer'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$7*100 :1/0):(sqrt( $7 * (1-$7) / $2 )*100) w yerr lt 3 pt 7

set title 'Disinfecting Wipes'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$8*100 :1/0):(sqrt( $8 * (1-$8) / $2 )*100) w yerr lt 3 pt 7

set title 'Thermometers'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$9*100 :1/0):(sqrt( $9 * (1-$9) / $2 )*100) w yerr lt 3 pt 7

set title 'Disposable Booties'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$10*100:1/0):(sqrt( $10 * (1-$10) / $2 )*100) w yerr lt 3 pt 7

set title 'Face Shield'
pl [startDay:endDay][0:100] fn u 1:($2>statCutoff?$12*100:1/0):(sqrt( $12 * (1-$12) / $2 )*100) w yerr lt 3 pt 7

set title 'Face Shields (3D or maker)'
pl [startDay:endDay][0:100] fn u ($1>5?$1:1/0):($2>statCutoff?$13*100:1/0):(sqrt( $13 * (1-$13) / $2 )*100) w yerr lt 3 pt 7

unset ylabel 
set logsc y

set label 5 'low-statistics cutoff' at -3,(statCutoff*0.65) center front
set title 'Number of still-approved entries daily'
pl [startDay:endDay][1:]\
	fn u 1:2  w p lt -1 pt 7,\
	statCutoff lt 0

unset multiplot
